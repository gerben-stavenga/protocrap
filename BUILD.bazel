load("@rules_rust//rust:defs.bzl", "rust_library", "rust_binary", "rust_test")
load("//bazel:proto_descriptor_set.bzl", "proto_descriptor_set")

exports_files([
    "Cargo.toml",
    "Cargo.lock",
    ".cargo/config.toml",
])

# Main protocrap library
rust_library(
    name = "protocrap",
    srcs = glob(
        ["src/**/*.rs"],
        exclude = ["src/main.rs", "src/codegen/**"],
    ),
    crate_features = [
        "std",
        "serde_support",
    ],
    edition = "2024",
    visibility = ["//visibility:public"],
    deps = [
        "@crates//:allocator-api2",
        "@crates//:base64",
        "@crates//:futures",
        "@crates//:serde",
        "@crates//:time",
    ],
)

# Main protocrap library with codegen
rust_library(
    name = "protocrap-with-codegen",
    srcs = glob(
        ["src/**/*.rs"],
        exclude = ["src/main.rs"],
    ),
    crate_features = [
        "std",
        "serde_support",
        "codegen",
    ],
    edition = "2024",
    visibility = ["//visibility:public"],
    deps = [
        "@crates//:allocator-api2",
        "@crates//:base64",
        "@crates//:futures",
        "@crates//:serde",
        "@crates//:serde_json",
        "@crates//:time",
        "@crates//:anyhow",
        "@crates//:prettyplease",
        "@crates//:proc-macro2",
        "@crates//:quote",
        "@crates//:syn",
    ],
)

# Protocrap codegen binary
rust_binary(
    name = "protocrap-codegen",
    srcs = ["src/main.rs"] + glob(["src/codegen/*.rs"]),
    crate_features = [
        "std",
        "codegen",
        "bazel",
    ],
    edition = "2024",
    visibility = ["//visibility:public"],
    deps = [
        ":protocrap-with-codegen",
        "@crates//:anyhow",
    ],
)

# Descriptor proto for codegen bootstrap
proto_descriptor_set(
    name = "descriptor_set",
    deps = [
        "@protobuf//:descriptor_proto",
    ],
    visibility = ["//visibility:public"],
)

# Unit tests for the main library
rust_test(
    name = "protocrap_test",
    crate = ":protocrap",
)

# Test that descriptor.pc.rs is up to date with codegen
genrule(
    name = "generate_descriptor_test",
    srcs = [":descriptor_set"],
    outs = ["descriptor.pc.rs.generated"],
    cmd = "$(location :protocrap-codegen) $(location :descriptor_set) $@",
    tools = [":protocrap-codegen"],
)

sh_test(
    name = "descriptor_up_to_date_test",
    srcs = ["test_descriptor_up_to_date.sh"],
    data = [
        ":generate_descriptor_test",
        "src/descriptor.pc.rs",
    ],
)

